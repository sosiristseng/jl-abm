{
 "cells": [
  {
   "cell_type": "markdown",
   "source": [
    "# Event-driven continuous-time multiagent model\n",
    "\n",
    "The spatial rock-paper-scissors (RPS) is an ABM with the following rules:\n",
    "\n",
    "+ Agents can be any of three \"kinds\": Rock, Paper, or Scissors.\n",
    "+ Agents live in a 2D periodic grid space allowing only one agent per cell.\n",
    "+ When an agent activates, it can do one of three actions:\n",
    "    1. Attack: choose a random nearby agent and attack it. If the agent loses the RPS game it gets removed.\n",
    "    2. Move: choose a random nearby position. If it is empty move to it, otherwise swap positions with the agent there.\n",
    "    3. Reproduce: choose a random empty nearby position (if any exist). Generate there a new agent of the same type."
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "using Agents\n",
    "using Random\n",
    "using LinearAlgebra\n",
    "using Base64\n",
    "using Agents.DataFrames\n",
    "using CairoMakie\n",
    "CairoMakie.activate!(px_per_unit = 1.0)"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "The helper function is adapted from `Agents.abmvideo` and correctly displays animations in Jupyter notebooks"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "function abmvio(model;\n",
    "    dt = 1, framerate = 30, frames = 300, title = \"\", showstep = true,\n",
    "    figure = (size = (600, 600),), axis = NamedTuple(),\n",
    "    recordkwargs = (compression = 23, format =\"mp4\"), kwargs...\n",
    ")\n",
    "    # title and steps\n",
    "    abmtime_obs = Observable(abmtime(model))\n",
    "    if title â‰  \"\" && showstep\n",
    "        t = lift(x -> title*\", time = \"*string(x), abmtime_obs)\n",
    "    elseif showstep\n",
    "        t = lift(x -> \"time = \"*string(x), abmtime_obs)\n",
    "    else\n",
    "        t = title\n",
    "    end\n",
    "\n",
    "    axis = (title = t, titlealign = :left, axis...)\n",
    "    # First frame\n",
    "    fig, ax, abmobs = abmplot(model; add_controls = false, warn_deprecation = false, figure, axis, kwargs...)\n",
    "    resize_to_layout!(fig)\n",
    "    # Animation\n",
    "    Makie.Record(fig; framerate, recordkwargs...) do io\n",
    "        for j in 1:frames-1\n",
    "            recordframe!(io)\n",
    "            Agents.step!(abmobs, dt)\n",
    "            abmtime_obs[] = abmtime(model)\n",
    "        end\n",
    "        recordframe!(io)\n",
    "    end\n",
    "end"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "Define rock, paper, and scissors (RPS) agents. One can use `variant(agent)` to see the agent type."
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "@agent struct Rock(GridAgent{2}) end\n",
    "@agent struct Paper(GridAgent{2}) end\n",
    "@agent struct Scissors(GridAgent{2}) end\n",
    "@multiagent RPS(Rock, Paper, Scissors)"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "Agent actions"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "function attack!(agent, model)\n",
    "    # Randomly pick a nearby agent\n",
    "    contender = random_nearby_agent(agent, model)\n",
    "    isnothing(contender) && return # do nothing if there isn't anyone nearby\n",
    "    # The attacking action will be dispatched to the following methods.\n",
    "    attack!(variant(agent), variant(contender), contender, model)\n",
    "    return nothing\n",
    "end\n",
    "\n",
    "attack!(::AbstractAgent, ::AbstractAgent, contender, model) = nothing\n",
    "attack!(::Rock, ::Scissors, contender, model) = remove_agent!(contender, model)\n",
    "attack!(::Scissors, ::Paper, contender, model) = remove_agent!(contender, model)\n",
    "attack!(::Paper, ::Rock, contender, model) = remove_agent!(contender, model)"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "Move actions use `move_agent!` and `swap_agents!` functions"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "function move!(agent, model)\n",
    "    rand_pos = random_nearby_position(agent.pos, model)\n",
    "    if isempty(rand_pos, model)\n",
    "        move_agent!(agent, rand_pos, model)\n",
    "    else\n",
    "        occupant_id = id_in_position(rand_pos, model)\n",
    "        occupant = model[occupant_id]\n",
    "        swap_agents!(agent, occupant, model)\n",
    "    end\n",
    "    return nothing\n",
    "end"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "Reproduce actions use `replicate!` function"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "function reproduce!(agent, model)\n",
    "    pos = random_nearby_position(agent, model, 1, pos -> isempty(pos, model))\n",
    "    isnothing(pos) && return\n",
    "    # pass target position as a keyword argument\n",
    "    replicate!(agent, model; pos)\n",
    "    return nothing\n",
    "end"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "Defining the propensity (\"rate\" in Gillespie stochastic simulations) and timing of the events"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "attack_propensity = 1.0\n",
    "movement_propensity = 0.5\n",
    "reproduction_propensity(agent, model) = cos(abmtime(model))^2"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "Register events with `AgentEvent`"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "attack_event = AgentEvent(action! = attack!, propensity = attack_propensity)\n",
    "reproduction_event = AgentEvent(action! = reproduce!, propensity = reproduction_propensity)"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "We want a different distribution other than exponential distribution for movement time"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "function movement_time(agent, model, propensity)\n",
    "    # Make time around 1\n",
    "    t = 0.1 * randn(abmrng(model)) + 1\n",
    "    return clamp(t, 0, Inf)\n",
    "end"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "Also rocks do not move"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "movement_event = AgentEvent(\n",
    "    action! = move!, propensity = movement_propensity,\n",
    "    types = Union{Scissors, Paper}, timing = movement_time\n",
    ")"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "Collect all events"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "events = (attack_event, reproduction_event, movement_event)"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "Model function `EventQueueABM` for an event-driven ABM"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "const alltypes = (Rock, Paper, Scissors)\n",
    "\n",
    "function initialize_rps(; n = 100, nx = n, ny = n, seed = 42)\n",
    "    space = GridSpaceSingle((nx, ny))\n",
    "    rng = Xoshiro(seed)\n",
    "    model = EventQueueABM(RPS, events, space; rng, warn = false)\n",
    "    for p in positions(model)\n",
    "        # Randomly assign one of the agent\n",
    "        type = rand(abmrng(model), alltypes)\n",
    "        add_agent!(p, constructor(RPS, type), model)\n",
    "    end\n",
    "    return model\n",
    "end"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "Have a look at the event queue"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "model = initialize_rps()\n",
    "abmqueue(model)"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "The time in `EventQueueABM` is continuous, so we can pass real-valued time."
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "step!(model, 123.456)\n",
    "nagents(model)"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "The `step!` function also accepts a terminating condition."
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "function terminate(model, t)\n",
    "    willterm = length(allagents(model)) < 5000\n",
    "    return willterm || (t > 1000.0)\n",
    "end\n",
    "\n",
    "model = initialize_rps()\n",
    "step!(model, terminate)\n",
    "abmtime(model)"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "## Data collection\n",
    "- adata: aggregated data to extract information from the execution stats\n",
    "- adf: agent data frame"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "model = initialize_rps()\n",
    "adata = [(a -> variantof(a) === X, count) for X in alltypes]\n",
    "\n",
    "adf, mdf = run!(model, 100.0; adata, when = 0.5, dt = 0.01)\n",
    "adf[1:10, :]"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "## Visualize population change"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "tvec = adf[!, :time]  ## time as x axis\n",
    "populations = adf[:, Not(:time)]  ## agents as data\n",
    "alabels = [\"rocks\", \"papers\", \"scissors\"]\n",
    "\n",
    "fig = Figure();\n",
    "ax = Axis(fig[1,1]; xlabel = \"time\", ylabel = \"population\")\n",
    "for (i, l) in enumerate(alabels)\n",
    "    lines!(ax, tvec, populations[!, i]; label = l)\n",
    "end\n",
    "axislegend(ax)\n",
    "fig"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "## Visualize agent distribution"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "const colormap = Dict(Rock => \"black\", Scissors => \"gray\", Paper => \"orange\")\n",
    "agent_color(agent) = colormap[variantof(agent)]\n",
    "plotkw = (agent_color, agent_marker = :rect, agent_size = 5)\n",
    "fig, ax, abmobs = abmplot(model; plotkw...)\n",
    "\n",
    "fig"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "# Animation"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "model = initialize_rps()\n",
    "vio = abmvio( model;\n",
    "    dt = 0.5, frames = 300,\n",
    "    title = \"Rock Paper Scissors (event based)\",\n",
    "    plotkw...,\n",
    ")\n",
    "\n",
    "save(\"rps.mp4\", vio)\n",
    "vio |> display"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "---\n",
    "\n",
    "*This notebook was generated using [Literate.jl](https://github.com/fredrikekre/Literate.jl).*"
   ],
   "metadata": {}
  }
 ],
 "nbformat_minor": 3,
 "metadata": {
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.11.2"
  },
  "kernelspec": {
   "name": "julia-1.11",
   "display_name": "Julia 1.11.2",
   "language": "julia"
  }
 },
 "nbformat": 4
}
